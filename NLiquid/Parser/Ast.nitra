using Nitra;
using Nitra.Declarations;
using Nitra.Runtime;

using NLiquid;

using System.IO;
using System.Collections.Immutable;

namespace NLiquid.Parser.Ast
{
	ast CompilationUnit : BindableAst, LocalVariableScope
	{
		Statements.CurrentTable = LocalVariables;
		Statements.Scope = Scope.HideWith(LocalVariables);

		Statements : Statement*;
	}

	abstract ast LocalVariableScope
	{
		out LocalVariables : TableScope = TableScope("Local variables");
	}

	abstract ast Statement : BindableAst
	{
		in CurrentTable : TableScope;

		| Plain  { }
		| Comment { }
		| Output
		 {
			Expr.Scope = Scope;

			Expr : Expr;
		 }
		| If : LocalVariableScope
		 {
			CurrentTable         = LocalVariables;
			// протаскиваем текущую таблицу имен во вложенные блоки
			Body.CurrentTable    = CurrentTable;
			ElseIfs.CurrentTable = CurrentTable;
			Else.CurrentTable    = CurrentTable;
			Body.Scope           = Scope.HideWith(LocalVariables); // скрываем скопом локальных переменных скопы сформированные выше.
			Condition.Scope      = Scope;
			ElseIfs.Scope        = Scope;
			Else.Scope           = Scope;

			Condition : Expr;
			Body      : Statement*;
			ElseIfs   : ElseIf*;
			Else      : Else?;
		 }
		| Unless : LocalVariableScope
		{
			CurrentTable      = LocalVariables;
			Body.CurrentTable = CurrentTable;
			Body.Scope        = Scope.HideWith(LocalVariables);
			Condition.Scope   = Scope;

			Condition : Expr;
			Body      : Statement*;
		}
	}

	ast ElseIf : Statement, LocalVariableScope
	{
		CurrentTable      = LocalVariables;
		Body.CurrentTable = CurrentTable;
		Body.Scope        = Scope.HideWith(LocalVariables);
		Condition.Scope   = Scope;

		Condition : Expr;
		Body      : Statement*;
	}

	ast Else : Statement, LocalVariableScope
	{
		CurrentTable      = LocalVariables;
		Body.CurrentTable = CurrentTable;
		Body.Scope        = Scope.HideWith(LocalVariables);

		Body: Statement*;
	}

	abstract declaration LocalVariable : Statement
	{
		symbol
		{
			in Type: TypeSymbol;
		}

		ContainingTable   = CurrentTable;

		| SimpleLocalVariable
		  {
			  Symbol.Kind       = "assigment";
			  Symbol.Type       = Initializer.Type;
			  Initializer.Scope = Scope;

			  Initializer : Expr;
		  }
		| CaptureLocalVariable
		  {
			  Symbol.Kind       = "capture";
			  Symbol.Type       = context.GetGlobalTypes().String;
			  Body.Scope        = Scope;

			  Body: Statement*;
		  }
	}

	abstract ast Binary : Expr
	{
		Expr1.Scope = Scope;
		Expr2.Scope = Scope;

		Expr1 : Expr;
		Expr2 : Expr;
	}

	abstract ast SameTypesExpr : Binary
	{
		unless (NLiquidTypeUnifier.Instance.TryUnify(Expr1.Type, Expr2.Type))
			Expr2.Error(context, $"$(self.Expr2.Type) is not compatible with $(self.Expr1.Type).");
	}

	abstract ast EqualExpr : SameTypesExpr
	{
		Type = context.GetGlobalTypes().Boolean;
	}

	abstract ast OrAndExpr : SameTypesExpr
	{
		Type = context.GetGlobalTypes().Boolean;
	}

	abstract ast ContainsExpr : SameTypesExpr
	{
		Type = context.GetGlobalTypes().Boolean;

		unless (NLiquidTypeUnifier.Instance.TryUnify(Expr1.Type, context.GetGlobalTypes().String))
			Expr1.Error(context, $"Expected string expression but found $(self.Expr1.Type).");
	}

	abstract ast Expr : BindableAst
	{
		out Type        : TypeSymbol = context.GetGlobalTypes().Nil;
		in ExpectedType : TypeSymbol = NLiquidTypeUnifier.Instance.CreateTypeVar();

		unless (NLiquidTypeUnifier.Instance.TryUnify(Type, ExpectedType))
			Error(context, $"Expected $(self.ExpectedType) but got $(self.Type)");

		| True
		{
			Type = context.GetGlobalTypes().Boolean;
			Value : bool;
		}
		| False
		{
			Type = context.GetGlobalTypes().Boolean;
			Value : bool;
		}
		| Number
		{
			Type = context.GetGlobalTypes().Number;
			Value : double;
		}
		| SStr
		{
			Type = context.GetGlobalTypes().String;
			Value : string;
		}
		| DStr
		{
			Type = context.GetGlobalTypes().String;
			Value : string;
		}
		| LocalVariableRef
		{
			out Ref: Ref[LocalVariableSymbol] = Reference.Ref.Resolve();
			Reference.Scope = Scope;
			Type = Ref.Symbol.Type;
			Reference : Reference;
		}
		| MemberAccess
		{
			Qualifier.Scope = Scope;
			Reference.Scope = Scope;

			Qualifier : Expr;
			Reference : Reference;
		}
		| Error        { Error(context, "Expected: Expr '}}'"); }
		| Call
		{
			out Ref : Ref[FunSymbol] = Helpers.ResolveCall(FanName.Ref, Arg0.Type, Args.Type);

			FanName.Scope = Scope;
			Arg0.Scope    = Scope;
			Args.Scope    = Scope;

			FanName : Reference;
			Arg0    : Expr;
			Args    : Expr*;
		}
		| Or           : OrAndExpr {}
		| And          : OrAndExpr {}
		| Contains     : ContainsExpr {}
		| Equal        : EqualExpr{ }
		| NotEqual     : EqualExpr{ }
	}

	abstract ast TypeReference
	{
		in Type : TypeSymbol;

		| Nil        { Type = context.GetGlobalTypes().Nil;        }
		| Number     { Type = context.GetGlobalTypes().Number;     }
		| Boolean    { Type = context.GetGlobalTypes().Boolean;    }
		| String     { Type = context.GetGlobalTypes().String;     }
		| Object     { Type = context.GetGlobalTypes().Object;     }
		| Array      { Type = context.GetGlobalTypes().Array;      }
		| Dictionary { Type = context.GetGlobalTypes().Dictionary; }
	}

	declaration Root        : Container {}

	declaration Nil         : Type {}
	declaration Number      : Type {}
	declaration Boolean     : Type {}
	declaration String      : Type {}
	declaration Object      : Type {}
	declaration Array       : Type {}
	declaration Dictionary  : Type {}
	declaration Fun			: Type
	{
		symbol
		{
			in Parameters : ImmutableArray[TypeSymbol];
			in Result     : TypeSymbol;

			Kind = "function type";
		}
	}
}
