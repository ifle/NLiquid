using Nemerle;
//using Nemerle.Linq;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Linq;
using System.Reflection;
using System.Collections.Generic;
using System.Collections.Immutable;

using Nitra;
using Nitra.Declarations;

using NLiquid;
using NLiquid.Parser;
using NLiquid.Parser.Ast;

namespace NLiquid.Filters
{
  public class FilterTypes
  {
    public Filters: ImmutableArray[FunSymbol];
    
    public this(filters: ImmutableArray[FunSymbol])
    {
        Filters = filters;
    }
    public static Create(scope: TableScope, globalTypes: NLiquidGlobalTypes): FilterTypes
    {
      def builder       = ImmutableArray.CreateBuilder.[FunSymbol]();
      def filterMethods = LoadFilterMethods();
      
      foreach (filterMethod in filterMethods)
      {
        def filterSymbol = CreateFilterSymbol(filterMethod, scope, globalTypes);
        when(filterSymbol != null)
          builder.Add(filterSymbol);
      }
        
      
      def filters = builder.ToImmutable();
      FilterTypes(filters);
    }
    
    // laod filter functions marked with FilterAttribute 
    static LoadFilterMethods(): ImmutableArray[MethodInfo]
    {
      def builder = ImmutableArray.CreateBuilder.[MethodInfo]();
      
      
      foreach (filterType in Assembly.GetExecutingAssembly().GetTypes().Where(type => type.IsClass && type.GetCustomAttributesData().Any(attr => attr.AttributeType == typeof(FilterSourceAttribute))))
		  foreach (method in filterType.GetMethods(BindingFlags.Public | BindingFlags.Static))
			{
				builder.Add(method);			  
			}
			
			builder.ToImmutable();      
    }
    
    // create funsymbol from memberinfo of filter
    static CreateFilterSymbol(methodInfo: MethodInfo, scope: TableScope, globalTypes: NLiquidGlobalTypes): FunSymbol
    {
      def filterAttribute = methodInfo.GetCustomAttributesData().FirstOrDefault(attr => attr.AttributeType == typeof(FilterAttribute));
      if(filterAttribute != null)
      {
        def filterName      = filterAttribute.ConstructorArguments[0].Value :> string;
        def symbol          = FilterFunDeclaration(Helpers.CreateName(filterName), methodInfo).DefineSymbol(scope) :> FunSymbol;
        def parameters      = methodInfo.GetParameters().Select(p => Helpers.CreateTypeSymbol(p.ParameterType, globalTypes)).ToImmutableArray();
        def returnType      = Helpers.CreateTypeSymbol(methodInfo.ReturnType, globalTypes);
      
			  symbol.Parameters = parameters;
			  symbol.Result     = returnType;
			  symbol;
      }
      else
      {
        null;  
      }    
    }
  }
}
